// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        versions = [
                kotlin    : "1.7.10",
                compileSdk: 32,
                minSdk    : 16,
                targetSdk : 32,
        ]

        gitCommitNumber = { ->
            try {
                def hashStdOut = new ByteArrayOutputStream()
                exec {
                    commandLine "git", "rev-list", "HEAD", "--count"
                    standardOutput = hashStdOut
                }
                return hashStdOut.toString().trim().toInteger()
            } catch (ignored) {
                return 1
            }
        }

        gitLastTag = { ->
            try {
                def outputStream = new ByteArrayOutputStream()
                exec {
                    commandLine "git", "describe", "--abbrev=0", "--tags"
                    standardOutput = outputStream
                }
                return outputStream.toString().trim()
            } catch (ignored) {
                return "1.0.0"
            }
        }
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
